plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'application'
    id 'com.moowork.node' version '0.11'
    id 'com.bmuschko.docker-remote-api' version '3.0.2'
    id 'com.github.kt3k.coveralls' version '2.5.0'
    id 'cz.malohlava' version '1.0.1'
    id 'net.researchgate.release' version '2.3.4'
}

group 'org.kaakaa.ppt-museum'
mainClassName = 'org.kaakaa.pptmuseum.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.sparkjava:spark-template-jade:2.3'
    compile 'org.mongodb:mongo-java-driver:2.14.1'
    compile 'org.mongodb.morphia:morphia:1.0.0-rc0'
    compile 'net.rakugakibox.util:yaml-resource-bundle:1.1'
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.apache.httpcomponents:httpmime:4.5.1'
    compile 'org.apache.pdfbox:pdfbox:2.0.0'
    compile 'org.bouncycastle:bcprov-jdk15on:1.54'
    compile 'org.bouncycastle:bcmail-jdk15on:1.54'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.54'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.slf4j:slf4j-api:1.7.20'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'ch.qos.logback:logback-core:1.1.7'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.zeroturnaround:zt-zip:1.9'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources' // default
            srcDir 'src/main/web/dist_packages' // frontend packages
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'ppt-museum'
        attributes 'Implementation-Version': version
        attributes 'Main-Class': mainClassName
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Built-JDK': System.getProperty('java.version')
        attributes 'Built-Time': new Date().format('yyyy-MM-dd HH:mm:ssZ')
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

test {
    finalizedBy jacocoTestReport
}

build.dependsOn installDist

// for installing node
node {
    version = '4.4.0'
    download = true
    workDir = file("${project.projectDir}/src/main/web")
    nodeModulesDir = file("${project.projectDir}/src/main/web")
}

task npmRunBuild(type: NpmTask) {
    dependsOn 'npmInstall'
    args = ['run', 'build']
    execOverrides {
        it.workingDir = 'src/main/web'
    }
}

processResources.dependsOn 'npmRunBuild'

// build Dockerfile
docker {
    url = 'unix:///var/run/docker.sock'
}


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

installDist.dependsOn assemble
task buildDockerImage(type: DockerBuildImage, group: 'Docker Tasks', description: 'Create docker images for this app') {
    dependsOn clean, installDist
    inputDir = rootDir
    tag = 'kaakaa/ppt-museum-web'
}

// for graph generator
visteg {
    enabled = true
    colouredNodes = true
    colouredEdges = true
    destination = "${buildDir}/reports/visteg.dot"
    exporter = 'dot'
    colorscheme = 'spectral11'
    nodeShape = 'box'
    startNodeShape = 'hexagon'
    endNodeShape = 'doubleoctagon'
}
