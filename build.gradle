plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'application'
    id 'maven'
    id 'com.moowork.grunt' version '0.11'
    id 'com.moowork.node' version '0.11'
    id 'com.bmuschko.docker-remote-api' version '2.6.6'
    id 'com.github.kt3k.coveralls' version '2.5.0'
    id 'cz.malohlava' version '1.0.1'
}

group 'org.kaakaa.ppt-museum'
version '0.2.1'

mainClassName = 'org.kaakaa.pptmuseum.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.sparkjava:spark-template-jade:2.3'
    compile 'org.mongodb:mongo-java-driver:2.14.1'
    compile 'org.mongodb.morphia:morphia:1.0.0-rc0'
    compile 'net.rakugakibox.util:yaml-resource-bundle:1.1'
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.apache.httpcomponents:httpmime:4.5.1'
    compile 'org.apache.pdfbox:pdfbox:2.0.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.slf4j:slf4j-api:1.7.20'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'ch.qos.logback:logback-core:1.1.7'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.zeroturnaround:zt-zip:1.9'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources' // default
            srcDir 'src/main/web/dist_packages' // frontend packages
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'ppt-museum', 'Implementation-Version': version
        attributes 'Main-Class': mainClassName
    }
}

artifacts {
    archives jar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${projectDir}/mavenRepo")
        }
    }
}

[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2016'
        packaging 'jar'
        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

test {
    finalizedBy jacocoTestReport
}

build.dependsOn installDist

// for installing node
node {
    version = '4.4.0'
    download = true
    workDir = file("${project.projectDir}/src/main/web")
    nodeModulesDir = file("${project.projectDir}/src/main/web")
}

// grunt
grunt {
    workDir = file("${project.projectDir}/src/main/web")
}

grunt_build.dependsOn 'npmInstall'
processResources.dependsOn grunt_build

// build Dockerfile
docker {
    url = 'unix:///var/run/docker.sock'
}


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

installDist.dependsOn assemble
task buildDockerImage(type: DockerBuildImage) {
    dependsOn clean, installDist
    inputDir = rootDir
    tag = 'kaakaa/ppt-museum-web'
}

// for graph generator
visteg {
    enabled		= true
    colouredNodes	= true
    colouredEdges	= true
    destination		= "${buildDir}/reports/visteg.dot"
    exporter		= 'dot'
    colorscheme		= 'spectral11'
    nodeShape		= 'box'
    startNodeShape	= 'hexagon'
    endNodeShape	= 'doubleoctagon'
}
